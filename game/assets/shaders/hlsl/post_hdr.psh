//--------------------------------------------------------------------------------------
// GLOBAL VARIABLES
//--------------------------------------------------------------------------------------

// fExposure;         // A user configurable bias to under/over expose the image
// fGaussianScalar;   // Used in the post-processing, but also useful here
// g_rcp_bloom_tex_w; // The reciprocal WIDTH of the texture in 'bloom'
// g_rcp_bloom_tex_h; // The reciprocal HEIGHT of the texture in 'bloom'
float4 gHDRParameter;

Texture2D gMainTexture;
SamplerState gMainTextureState;
Texture2D gLuminanceTexture;
SamplerState gLuminanceTextureState;
Texture2D gBloomTexture;
SamplerState gBloomTextureState;

//--------------------------------------------------------------------------------------
// STRUCTURES
//--------------------------------------------------------------------------------------
struct PS_INPUT
{
    float4 Position : SV_POSITION;
    float2 TexCoord : TEXCOORD0;
};

struct PS_OUTPUT
{
    float4 RGBColor : SV_TARGET;
};

//--------------------------------------------------------------------------------------
// PIXEL SHADER
//--------------------------------------------------------------------------------------

PS_OUTPUT PSMain( PS_INPUT In )
{ 
    PS_OUTPUT output;
	
	// Read the HDR value that was computed as part of the original scene
    float4 c = gMainTexture.Sample( gMainTextureState, In.TexCoord );
    
    // Read the luminance value, target the centre of the texture
    // which will map to the only pixel in it!
    float4 l = gLuminanceTexture.Sample( gLuminanceTextureState, float2( 0.5f, 0.5f ) );
        
    // Compute the blur value using a bilinear filter
    // It is worth noting that if the hardware supports linear filtering of a
    // floating point render target that this step can probably be skipped.
	float texCoord = In.TexCoord.x;
	float parameterz = gHDRParameter.z;
	float division = texCoord / parameterz;
	float xwfrac = frac( division );
    float xWeight = xwfrac - 0.5;
    float xDir = xWeight;
    xWeight = abs( xWeight );
    xDir /= xWeight;
    xDir *= gHDRParameter.z;

    float yWeight = frac( In.TexCoord.y / gHDRParameter.w ) - 0.5;
    float yDir = yWeight;
    yWeight = abs( yWeight );
    yDir /= yWeight;
    yDir *= gHDRParameter.w;

    // sample the blur texture for the 4 relevant pixels, weighted accordingly
    float4 b = ((1.0f - xWeight) * (1.0f - yWeight))    * gBloomTexture.Sample( gBloomTextureState, In.TexCoord );        
    b +=       (xWeight * (1.0f - yWeight))             * gBloomTexture.Sample( gBloomTextureState, In.TexCoord + float2( xDir, 0.0f ) );
    b +=       (yWeight * (1.0f - xWeight))             * gBloomTexture.Sample( gBloomTextureState, In.TexCoord + float2( 0.0f, yDir ) );
    b +=       (xWeight * yWeight)                      * gBloomTexture.Sample( gBloomTextureState, In.TexCoord + float2( xDir, yDir ) );
	
    // Compute the actual colour:
    float4 final = c + 0.25f * b;

	//float4 b = gBloomTexture.Sample( gBloomTextureState, In.TexCoord );        
	//float4 final = c + b;
    
            
    // Reinhard's tone mapping equation (See Eqn#3 from 
    // "Photographic Tone Reproduction for Digital Images" for more details) is:
    //
    //      (      (   Lp    ))
    // Lp * (1.0f +(---------))
    //      (      ((Lm * Lm)))
    // -------------------------
    //         1.0f + Lp
    //
    // Lp is the luminance at the given point, this is computed using Eqn#2 from the above paper:
    //
    //        exposure
    //   Lp = -------- * HDRPixelIntensity
    //          l.r
    //
    // The exposure ("key" in the above paper) can be used to adjust the overall "balance" of 
    // the image. "l.r" is the average luminance across the scene, computed via the luminance
    // downsampling process. 'HDRPixelIntensity' is the measured brightness of the current pixel
    // being processed.
    
    float Lp = (gHDRParameter.x / l.r) * max( final.r, max( final.g, final.b ) );
    
    // A slight difference is that we have a bloom component in the final image - this is *added* to the 
    // final result, therefore potentially increasing the maximum luminance across the whole image. 
    // For a bright area of the display, this factor should be the integral of the bloom distribution 
    // multipled by the maximum value. The integral of the gaussian distribution between [-1,+1] should 
    // be AT MOST 1.0; but the sample code adds a scalar to the front of this, making it a good enough
    // approximation to the *real* integral.
    
    float LmSqr = (l.g + gHDRParameter.y * l.g) * (l.g + gHDRParameter.y * l.g);
    
    // Compute Eqn#3:
    float toneScalar = ( Lp * ( 1.0f + ( Lp / ( LmSqr ) ) ) ) / ( 1.0f + Lp );
    
    // Tonemap the final outputted pixel:
    c = final * toneScalar;
    
    // Return the fully composed colour
    c.a = 1.0f;

    output.RGBColor = c;

    return output;  
}